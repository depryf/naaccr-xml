import java.time.Duration

/**
 * In addition to releasing the library on Maven Central, we also release a SAS library along with some macros.
 * Since the release is triggered on GitHub via the creation of a release, the SAS zip distribution has to be 
 * manually created before and added to the release:
 * 
 *     gradlew clean createDistribution
 * 
 * Note that creating a release doesn't seem to trigger the publish job, I am not sure why. I have been doing it manually so far...
 */

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.0.20'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0' // publish to Maven Central
    id 'com.github.ben-manes.versions' version '0.51.0' // check for out-of-date dependencies (run 'dependencyUpdates' manually)
    id 'org.sonatype.gradle.plugins.scan' version '2.8.3' // scan for vulnerabilities
    id "org.sonarqube" version "5.1.0.4882"// sonarQube analysis
}

group = 'com.imsweb'
version = file('VERSION').text
description = 'This library provides support for the NAACCR XML format.'

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.thoughtworks.xstream:xstream:1.4.20'

    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-lang3:3.16.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.imsweb:data-generator:1.32'
    testImplementation 'com.imsweb:layout:5.6'
    testImplementation 'com.opencsv:opencsv:5.9'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// generate javadoc and sources (required by Nexus)
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

// I have absolutely no idea why I need to do this; it appears the test task depends on the created JAR file; I know it depends 
// on the source code, but I can't figure out why it depends on the created archive itself. I found this workaround online...
tasks.named("test") {
    dependsOn(tasks.named("jar"))
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.naaccrxml')
    }
    from('VERSION') {
        rename 'VERSION', 'NAACCR-XML-VERSION'
    }
    from('LICENSE') {
        rename 'LICENSE', 'NAACCR-XML-LICENSE'
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_naaccr-xml"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions", "**/lab/**,**/sas/**"
        property "sonar.coverage.exclusions", "**/lab/**,**/sas/**"
    }
}

// Nexus vulnerability scan (see https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    outputFormat = 'DEPENDENCY_GRAPH'
    printBanner = false
}
check.dependsOn 'ossIndexAudit'

// checkstyle plugin settings
checkstyle {
    ignoreFailures = false
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/checkstyle-exclude.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = false
    excludeFilter.set(project(':').file("config/spotbugs/spotbugs-exclude.xml"))
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
test.finalizedBy jacocoTestReport

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// needed to deploy to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'naaccr-xml'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'NAACCR XML'
                description = 'This library provides support for the NAACCR XML format.'
                url = 'https://github.com/imsweb/naaccr-xml'
                inceptionYear = '2015'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/naaccr-xml/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'depryf'
                        name = 'Fabian Depry'
                        email = 'depryf@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/naaccr-xml'
                    connection = 'scm:https://github.com/imsweb/naaccr-xml.git'
                    developerConnection = 'scm:git@github.com:imsweb/naaccr-xml.git'
                }
            }
        }
    }
}

// setup JAR signing
signing {
    required { !project.version.endsWith('-SNAPSHOT') }

    String signingKey = project.findProperty('signingKey') ?: ''
    String signingPassword = project.findProperty('signingPassword') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}


// needed to release on maven central
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '63e5ddd3ab0d16'

            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(Duration.ofMillis(5000))
    }
}

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.ALL
}

// the SAS library is compiled under Java 7 code-compatibility; Intellij can't handle that (only because I didn't properly set it up as a module)
if (System.getProperty("idea.active") != 'true')
    apply from: 'gradle/sas.gradle'
