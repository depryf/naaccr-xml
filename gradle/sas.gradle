// this gradle file is used to create Java-7 compliant code to be used with SAS.

sourceSets {
    sas {
        java {
            srcDir "src/main/java/com/imsweb/naaccrxml/sas"
        }

        resources {
            srcDir "docs/"
            include "naaccr-xml-items-*.csv"
        }

        resources {
            srcDir "docs/grouped-items"
            include "naaccr-xml-grouped-items-*.csv"
        }
    }
}

compileSasJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

tasks.register('jarSas', Jar) {
    from sourceSets.sas.output
    archiveClassifier = 'sas'
    dependsOn compileSasJava
}

tasks.register('sourcesSasJar', Jar) {
    from sourceSets.sas.allSource
    archiveClassifier = 'sas-sources'
    dependsOn 'sasClasses'
}

tasks.register('assembleDistribution') {
    group = 'distribution'
    doLast {
        // SAS stuff
        copy {
            from "build/libs/${project.name}-${project.version}-sas.jar"
            from 'docs/sas/macro/read_naaccr_xml_macro.sas'
            from 'docs/sas/macro/write_naaccr_xml_macro.sas'
            into 'build/dist/sas'
        }
    }
    dependsOn 'assemble', 'jarSas', 'sourcesSasJar'
}

tasks.register('createDistribution', Zip) {
    group = 'distribution'
    archiveFileName = "naaccr-xml-utility-${project.version}.zip"
    destinationDirectory = file('build')
    from 'build/dist'
    into "naaccr-xml-utility-${project.version}"
    dependsOn 'assembleDistribution'
}